plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.7'
	id 'io.spring.dependency-management' version '1.1.0'

	//RestDocs - Asciidoctor 플러그인 적용
	id "org.asciidoctor.jvm.convert" version "3.3.2"
}

group = 'com.inq.wish-hair'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	asciidoctorExtensions // dependencies 에서 적용한 것 추가
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	// RestDocs - 아래서 사용할 변수
	snippetsDir = file('build/generated-snippets')
}

dependencies {
	//P6Spy
	implementation("com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0")

	//javax.annotation import 버그 해결
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	//jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	implementation 'javax.xml.bind:jaxb-api:2.3.0' //jwt 토큰 생성시 에러 해결

	//test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	//JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	//spring
	implementation 'org.springframework.boot:spring-boot-starter-web'

	//validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	//mail
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	compileOnly 'com.sun.mail:javax.mail'

	//lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok' // 테스트 의존성 추가
	testAnnotationProcessor 'org.projectlombok:lombok' // 테스트 의존성 추가

	//DB
	runtimeOnly 'com.mysql:mysql-connector-j'
	runtimeOnly 'com.h2database:h2' //h2 DB - Test 전용

	// RestDocs
	asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

	//네이버 클라우드
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

	//Spring Security
	implementation 'org.springframework.boot:spring-boot-starter-security'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	configurations 'asciidoctorExtensions'
	baseDirFollowsSourceFile()
	inputs.dir snippetsDir
	dependsOn test
}

asciidoctor.doFirst {
	delete file('src/main/resources/static/docs/*')
}

task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from file("build/docs/asciidoc")
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}